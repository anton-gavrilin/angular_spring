allprojects {
    repositories {
        mavenLocal()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = 17
    targetCompatibility = 17

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${spring_boot_gradle_plugin_version}")
        }
    }

    dependencies {
        implementation "com.fasterxml.jackson.core:jackson-annotations"
        implementation "com.fasterxml.jackson.core:jackson-core"
        implementation "com.fasterxml.jackson.core:jackson-databind"
        implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
        implementation("javax.inject:javax.inject:${javax_inject_version}")
        implementation("org.slf4j:slf4j-api")


        compileOnly("org.projectlombok:lombok:${lombok_version}")
        annotationProcessor("org.projectlombok:lombok:${lombok_version}")

        testCompileOnly("org.projectlombok:lombok:${lombok_version}")
        testAnnotationProcessor("org.projectlombok:lombok:${lombok_version}")

        testImplementation("org.assertj:assertj-core")
        testImplementation("org.mockito:mockito-core:${mockito_framework_version}")
        testImplementation("org.mockito:mockito-junit-jupiter:${mockito_framework_version}")
        testImplementation('org.junit.jupiter:junit-jupiter-api')
        testImplementation('org.junit.jupiter:junit-jupiter-engine')
        testImplementation('org.junit.jupiter:junit-jupiter-params')
        testImplementation('org.springframework.boot:spring-boot-starter-test')
    }

    test {
        useJUnitPlatform {
            excludeTags 'integration', 'api'
        }
    }

    task integrationTest(type: Test) {
        useJUnitPlatform {
            includeTags 'integration'
        }
    }

    task apiTest(type: Test) {
        useJUnitPlatform {
            includeTags 'api'
        }
    }
}
